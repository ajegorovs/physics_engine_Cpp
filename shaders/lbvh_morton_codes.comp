/**
* VkLBVH written by Mirco Werner: https://github.com/MircoWerner/VkLBVH
* Based on:
* https://research.nvidia.com/sites/default/files/pubs/2012-06_Maximizing-Parallelism-in/karras2012hpg_paper.pdf
* https://developer.nvidia.com/blog/thinking-parallel-part-iii-tree-construction-gpu/
* https://github.com/ToruNiina/lbvh
* https://github.com/embree/embree/blob/v4.0.0-ploc/kernels/rthwif/builder/gpu/sort.h
*/
#version 460
#extension GL_GOOGLE_include_directive: enable

#include "lbvh_common.glsl"

layout (local_size_x = 256) in;

layout (push_constant, std430) uniform PushConstants {
    uint g_num_elements;
};

struct GlobalBoundingBox {
    float minX;
    float minY;
    float minZ;
    float maxX;
    float maxY;
    float maxZ;
};


struct Particle {
	vec4 color;
	vec3 position;
	vec3 velocity;
	vec3 acceleration;
	vec3 bbmin;
	vec3 bbmax;
	float mass;
	float damping;
	float group_id;
};

layout (std430, set = 0, binding = 0) writeonly buffer morton_codes {
    MortonCodeElement g_morton_codes[];
};

layout (std430, set = 0, binding = 1) readonly buffer particles {
    Particle g_particles[];
};

layout (std430, set = 0, binding = 2) readonly buffer boundingbox {
    GlobalBoundingBox g_BB;
};

// Expands a 10-bit integer into 30 bits
// by inserting 2 zeros after each bit.
uint expandBits(uint v) {
    v = (v * 0x00010001u) & 0xFF0000FFu;
    v = (v * 0x00000101u) & 0x0F00F00Fu;
    v = (v * 0x00000011u) & 0xC30C30C3u;
    v = (v * 0x00000005u) & 0x49249249u;
    return v;
}

// Calculates a 30-bit Morton code for the
// given 3D point located within the unit cube [0,1].
uint morton3D(float x, float y, float z) {
    x = min(max(x * 1024.0f, 0.0f), 1023.0f);
    y = min(max(y * 1024.0f, 0.0f), 1023.0f);
    z = min(max(z * 1024.0f, 0.0f), 1023.0f);
    uint xx = expandBits(uint(x));
    uint yy = expandBits(uint(y));
    uint zz = expandBits(uint(z));
    return xx * 4 + yy * 2 + zz;
}

// calculate morton code for each element
void main() {
    uint gID = gl_GlobalInvocationID.x;

    if (gID >= g_num_elements) {
        return;
    }

    Particle p = g_particles[gID];
	
    // calculate center
    vec3 center =  0.5 *(p.bbmin + p.bbmax);
    // map to unit cube
    vec3 g_min = vec3(g_BB.minX, g_BB.minY, g_BB.minZ);
    vec3 g_max = vec3(g_BB.maxX, g_BB.maxY, g_BB.maxZ);
    vec3 mappedCenter = (center - g_min) / (g_max - g_min);
    // assign morton code
    g_morton_codes[gID] = MortonCodeElement(morton3D(mappedCenter.x, mappedCenter.y, mappedCenter.z), gID);
}